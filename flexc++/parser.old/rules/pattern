pattern:
    EOF_PATTERN
    {
        $$ = eofPattern();
    }
|
    STRING
    {                                                   
        $$ = PatternVal::str(d_states,
                        d_scanner.matched().substr(1, d_scanner.length() - 2));
    }
|
    DECIMAL
    {                                                   
        $$ = PatternVal::str(d_states, d_scanner.matched());
    }
|
    SECTION_DELIMITER
    {
        $$ = PatternVal::str(d_states, d_scanner.matched());
    }
|
    character_class
    {
        $$ = PatternVal::charSet(d_states, *$1);
    }
|
    plain_characters
    {
        $$ = PatternVal::plain(
                d_states, 
                static_cast<unsigned char>(d_scanner.matched()[0])
            );
    }
|
    ESCAPE_SEQUENCE
    {
        $$ = PatternVal::plain(d_states, d_scanner.matched());
    }
|
    '.'
    {
        $$ = PatternVal::dot(d_states);
    }
|
    pattern pattern         %prec CHARACTER
    {
        $$ = PatternVal::concatenate(d_states, *$1, *$2);
    }
|
    pattern '|' pattern
    {
        $$ = PatternVal::opOr(d_states, *$1, *$3);
    }
|
    pattern quantifier
    {
        $$ = PatternVal::quantifier(d_states, *$1, d_scanner.matched()[0]);
    }
|
    '(' incParen pattern ')' decParen
    {
        $$ = $3;
    }    
|
    pattern '{' start_interval_m  interval '}' regex_block_m
    {
        $$ = interval(*$1, *$4);
    }
//|
//    pattern '/' pattern         // handles $ through scanner intelligence
//    {
//        $$ = lookahead(*$1, *$3);
//    }
;

