_decimal:
    DECIMAL
    {
        cout << "IN _decimal\n";
        $$ = SemUnion(d_matched);
    }
;

_interval:
    _decimal
    {
        $$ = SemUnion(Interval::exact($1->value<TEXT>()));
    }
|
    _decimal ',' _decimal
    {
        $$ = SemUnion(Interval::range($1->value<TEXT>(), $3->value<TEXT>()));
    }
|
    _decimal ','
    {
        $$ = SemUnion(Interval::lowerBound($1->value<TEXT>()));
    }
;

_single_char:
    CHAR
|
    combi_chars
;

_open_paren:
    '('
    {
        ++d_parentheses;
    }
;

_close_paren:
    ')'
    {
        --d_parentheses;
    }
;

_open_curly:
    '{'
    {
        cout << "OPEN CURLY\n";
        d_scanner.acceptIdentifier();
    }
;

_interUnit:
    _unit _open_curly _interval '}' 
    {
        cout << "CLOSE CURLY\n";
        d_scanner.identifierAsChar();
        $$ = interval($1, $3);
    }
;

_unit:
    _interUnit
|
    '^'
    {
        $$ = boln();
    }
|
    '$'
    {
        $$ = dollar();
    }
|
    DOLLAR
    {
        $$ = eolnDollar();
    }
|
    STRING
    {
        $$ = str();
    }
|
    QUOTES
    {                                                   
        $$ = quotes();
    }
|
    _single_char
    {
        $$ = rawText();
    }
|
    ESCAPE_SEQUENCE
    {
        $$ = escape();
    }
|
    '.'
    {
        $$ = dot();
    }
|
    _unit _quantifier
    {
        $$ = quantifier($1);
    }
|
    characterclass
    {
        $$ = characterClass($1);
    }
|
    _open_paren regex _close_paren
    {
        $$ = $2;
    }
;

_or:
    _unit
|
    _or _unit
    {
        $$ = concatenate($1, $2);
    }
;

    // a regex is a regex, possibly with LA operator
    // the handler must verify whether the regex contains ^ and $ symbols
    // within the regex, for which a warning must be issue: ^, $ treated as
    // normal characters
    // regex returns a Pattern, embedded in a spSemUnion
regex:
    _or
|
    regex '|' regex
    { 
        $$ = alternatives($1, $3);
    }
|
    regex '/' regex
    {
        $$ = lookahead($1, $3);
    }
;







