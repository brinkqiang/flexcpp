_decimal:
    DECIMAL
    {
        $$ = SemUnion(d_matched);
    }
;

_interval:
    _decimal
    {
        $$ = SemUnion(Interval::exact($1->value<TEXT>()));
    }
|
    _decimal ',' _decimal
    {
        $$ = SemUnion(Interval::range($1->value<TEXT>(), $3->value<TEXT>()));
    }
|
    _decimal ','
    {
        $$ = SemUnion(Interval::lowerBound($1->value<TEXT>()));
    }
;

_single_char:
    CHAR
|
    combi_chars
;

_open_paren:
    '('
    {
        ++d_parentheses;
    }
;

_close_paren:
    ')'
    {
        --d_parentheses;
    }
;


_unit:
    _single_char
    {
        $$ = rawText();
    }
|
    '^'
    {
        $$ = boln();
    }
|
    '$'
    {
        $$ = dollar();
    }
|
    DOLLAR
    {
        $$ = eolnDollar();
    }
|
    STRING
    {
        $$ = str();
    }
|
    QUOTES
    {                                                   
        $$ = quotes();
    }
|
    ESCAPE_SEQUENCE
    {
        $$ = escape();
    }
|
    '.'
    {
        $$ = dot();
    }
|
    characterclass
    {
        $$ = characterClass($1);
    }
;
    
    // a regex is a regex, possibly with LA operator
    // the handler must verify whether the regex contains ^ and $ symbols
    // within the regex, for which a warning must be issue: ^, $ treated as
    // normal characters
    // regex returns a Pattern, embedded in a spSemUnion
regex:
    _unit                   %prec CHAR
|
    regex regex             %prec CHAR
    {
        $$ = concatenate($1, $2);
    }
|
    regex _quantifier       %prec '{'
    {
        $$ = quantifier($1);
    }
|
    regex '{' _interval '}'
    {
        $$ = interval($1, $3);
    }
|
    regex '|' regex
    { 
        $$ = alternatives($1, $3);
    }
|
    regex '/' regex
    {
        $$ = lookahead($1, $3);
    }
|
    _open_paren regex _close_paren
    {
        $$ = $2;
    }
;







