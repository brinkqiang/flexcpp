_decimal:
    DECIMAL
    {
        $$ = SemUnion(d_matched);
    }
;

_interval:
    _decimal
    {
        $$ = SemUnion(Interval::exact($1->value<TEXT>()));
    }
|
    _decimal ',' _decimal
    {
        $$ = SemUnion(Interval::range($1->value<TEXT>(), $3->value<TEXT>()));
    }
|
    _decimal ','
    {
        $$ = SemUnion(Interval::lowerBound($1->value<TEXT>()));
    }
;

_single_char:
    CHAR
|
    combi_chars
;

_open_paren:
    '('
    {
        ++d_parentheses;
    }
;

_close_paren:
    ')'
    {
        --d_parentheses;
    }
;

_open_curly:
    '{'
    {
        d_scanner.acceptMulti();
    }
;

_unit:
    '^'
    {
        $$ = boln();
    }
|
    '$'
    {
        $$ = dollar();
    }
|
    DOLLAR
    {
        $$ = eolnDollar();
    }
|
    STRING
    {
        $$ = str();
    }
|
    QUOTES
    {                                                   
        $$ = quotes();
    }
|
    ESCAPE_SEQUENCE
    {
        $$ = escape();
    }
|
    '.'
    {
        $$ = dot();
    }
|
    _single_char
    {
        $$ = rawText();
    }
|
    characterclass
    {
        $$ = characterClass($1);
    }
;

_quantifier:
    '*'
|
    '+'
|
    '?'
;

    // a regex is a regex, possibly with LA operator
    // the handler must verify whether the regex contains ^ and $ symbols
    // within the regex, for which a warning must be issue: ^, $ treated as
    // normal characters
    // regex returns a Pattern, embedded in a spSemUnion
_regex:
    _unit
|
    _regex _open_curly _interval '}'
    {
        $$ = interval($1, $3);
    }
|
    _regex _quantifier
    {
        $$ = quantifier($1);
    }
|
    _open_paren _regex _close_paren
    {
        $$ = $2;
    }
|
    _regex _regex                     %prec CHAR
    {
        $$ = concatenate($1, $2);
    }
|
    _regex '|' _regex
    { 
        $$ = alternatives($1, $3);
    }
|
    _regex '/' _regex
    {
        $$ = lookahead($1, $3);
    }
;

regexOrEOF:
    _regex
|
    EOF_PATTERN
    {
        $$ = eofPattern();
    }
;



