_miniscanner_list:
    _miniscanner_list ',' identifier
|
    identifier
;

_miniscanner_specs:
    '*'
|
    _miniscanner_list
;

_miniscanner:
    '<' _miniscanner_specs '>' 
        {cout << "MINI\n";}
;

    // elements may need to be split between cc_element and regex
    // if their handling turns out to be special
_combi_chars:
    STRING
|
    DECIMAL
|
    ESCAPE_SEQUENCE
|
    identifier
|
    CHAR
|
    '.'
|
    ','
|
    '^'
|
    '$'
|
    '='
|
    '-'
|
    '\''
;

_decimal:
    DECIMAL
    {
        $$ = d_matched;
    }
;

_interval:
    DECIMAL
|
    _decimal ',' DECIMAL
|
    _decimal ','
;
    

_quantifier:
    '*'
|
    '+'
|
    '?'
|
    '{' _interval '}'
;

_m_contents:
    {
        d_msg = "character class";
    }
;

_cc_start:
    CC_START
    _m_contents
;

_cc_negated:
    CC_NEGATED
    _m_contents
;

_cc_end:
    ']'
    {
        d_msg = "pattern or action block";
    }
;

    // characters handled identically within a character-set
_cc_char:
    '/'
|
    '|'
|
    '('
|
    ')'
|
    '*'
|
    '+'
|
    '?'
|
    '{'
|
    '}'
|
    '<'
|
    '>'
|
    '['
;

_cc_element:
    _combi_chars
|
    PREDEFINED_CLASS
|
    _cc_char
;


_cc_contents:
    _cc_contents _cc_element
|
    _cc_element
;


_cc_set:
    _cc_start _cc_contents _cc_end
|
    _cc_negated _cc_contents _cc_end
;


_characterclass:
    _characterclass CC_PLUS _characterclass
|
    _characterclass CC_MINUS _characterclass
|
    _cc_set
;
 

    // a regex is any regular expression, not having a LA operator
    // It may have ^ and $ symbols
_basic_regex:
    _combi_chars
|
    _basic_regex _basic_regex           %prec CHAR
|
    _basic_regex '|' _basic_regex
        { cout << "OR\n"; }
|
    '(' _basic_regex ')'
|
    _basic_regex _quantifier
        { cout << "QTY\n"; }
|
    _characterclass
        { cout << "CLASS\n"; }
;


    // the p_regex is a print-wrapper around regex
_p_regex:
    _basic_regex
        {cout << "RE\n";}
;

    // a _regex is a regex, possibly with LA operator
    // the handler must verify whether the regex contains ^ and $ symbols
    // within the regex, for which a warning must be issue: ^, $ treated as
    // normal characters
_ms_regex:
    _p_regex
|
    _p_regex '/' _p_regex 
        {cout << "LA\n";}
;


        // Regex in the form: <miniscanner>regex, The <miniscanner> is
        // optional
_opt_ms_regex:       
    _miniscanner _ms_regex
        {cout << "<MINI>RE\n";}
|
    _ms_regex
;

    // series of regexes, separated by '|' at the end of lines. E.g.,
    // regex1   |
    // regex2   |
    // regex3
    //
    // ORNL is returned at '| ws \n'
_regex_list:
    _regex_list ORNL _regex_list
        {cout << "ORNL\n";}
|
    _opt_ms_regex
;

_block:
    BLOCK
    {
        block();
    }
;

_ms_regex_list:
     _ms_regex_list ORNL _ms_regex_list
|
     _ms_regex
;

_ms_rule_def:
    // empty
|
    _ms_regex_list 
|
    _ms_regex_list _block    
|
    EOF_PATTERN
|
    EOF_PATTERN _block
;


    // each rule ends in a '\n'
_ms_rule:                      
    _ms_rule_def '\n'       
;                               
                                

_ms_rules:
    _ms_rules _ms_rule
|
    // empty
;


_miniscanner_block:
    _miniscanner '{' _ms_rules _ms_rule_def '}'
;


_rule_def:
    // only an empty line is OK
|
    // recovery from errors is OK: continue at the next line
    error       
|
        // just regular expressions, without an action block
    _regex_list 
|
        // the scanner returns a block if it encounters a non-blank character
        // on a line after ws, unless the character is a '|'
    _regex_list _block    
        { cout << "BLOCK\n"; }
|
    _miniscanner_block
|
    EOF_PATTERN
|
    EOF_PATTERN _block
;

    // each rule ends in a '\n'
_rule:                      
    _rule_def '\n'       
;                               
                                
    // there may be no rules
    // the scanner skips blanks, but returns '\n'
rules:
    rules _rule
|
    // empty
;

