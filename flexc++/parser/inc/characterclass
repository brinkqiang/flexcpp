_decimal:
    DECIMAL
    {
        $$ = SemUnion(d_matched);
    }
;

_interval:
    _decimal
    {
        $$ = SemUnion(Interval::exact($1->value<TEXT>()));
    }
|
    _decimal ',' _decimal
    {
        $$ = SemUnion(Interval::range($1->value<TEXT>(), 
                                      $3->value<TEXT>()));
    }
|
    _decimal ','
    {
        $$ = SemUnion(Interval::lowerBound($1->value<TEXT>()));
    }
;

_quantifier:
    '*'
|
    '+'
|
    '?'
;

_m_contents:
    {
        d_expect = "character class";
    }
;

_cc_start:
    CC_START
    _m_contents
    {
        $$ = setElements(1);   // skips the opening bracket
    }
;

_cc_negated:
    CC_NEGATED
    _m_contents
    {
        $$ = setElements(2);   // skips bracked and negation caret
    }
;

_cc_end:
    ']'
    {
        d_expect = "pattern or action block";
    }
;

    // characters handled identically within a character-set
_cc_literal:
    '/'
|
    '|'
|
    '('
|
    ')'
|
    '*'
|
    '+'
|
    '?'
|
    '{'
|
    '}'
|
    '<'
|
    '>'
|
    '['
|
    '.'
|
    '^'
|
    '$'
|
    DECIMAL
|
    IDENTIFIER
|
    CHAR
|
    STRING
|
    combi_chars
;

_cc_element:
    ESCAPE_SEQUENCE
    {
        $$ = CharClass::escape(d_matched);
    }
|
    PREDEFINED_CLASS
    {
       $$ = CharClass::predefined(d_matched);
    }
|
    _cc_literal
    {
        $$ = setElements();
    }
;

_cc_contents:
    _cc_contents _cc_element
    {
        $$ = setConcatenate($1, $2);
    }
|
    _cc_element
;

_cc_set:
    _cc_start _cc_contents _cc_end
    {
        $$ = setConcatenate($1, $2);
    }
|
    _cc_negated _cc_contents _cc_end
    {
        $$ = setNegate($1, $2);
    }
;

characterclass:
    characterclass CC_PLUS characterclass
    {
        $$ = unite($1, $3);
    }
|
    characterclass CC_MINUS characterclass
    {
        $$ = difference($1, $3);
    }
|
    _cc_set
;
